IF
	OnCreation()
	!Allegiance(Myself,EVILCUTOFF)
	!Global("RR#SMHOST","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		ChangeEnemyAlly(Myself,ALLY)
                DialogueInterrupt(FALSE)
		SetInterrupt(TRUE)
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	Global("RR#SMHOST","GLOBAL",1)
THEN
	RESPONSE #100
		Enemy()
END

IF
	Global("RR#SHMCHK","LOCALS",0)
	InParty(LastSummonerOf(Myself))
	Class(LastSummonerOf(Myself),DRUID_ALL)
	!Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetGlobal("RR#SHMCHK","LOCALS",1)
END

IF
	Global("RR#SHMCHK","LOCALS",0)
	InParty(LastSummonerOf(Myself))
	!Class(LastSummonerOf(Myself),DRUID_ALL)
	!Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("RR#SHMCHK","LOCALS",2)
		SetGlobal("RR#SMHOST","GLOBAL",1)
		PlaySound("EFF_M72D")
		FaceObject(LastSummonerOf(Myself))
		TakePartyItem("RR#GUARA") // The Guardian
		TakePartyItem("RR#GUARB") // The Guardian +2
		TakePartyItem("RR#GUARC") // The Guardian +3
                DestroyItem("RR#GUARA")
                DestroyItem("RR#GUARB")
                DestroyItem("RR#GUARC")
		DisplayStringNoNameHead(Myself,@66) // Without the guiding hand of a Druid, the enchantments on the shield disperse and the once trusted Guardian attacks its false summoner.
		Enemy()
		SetInterrupt(TRUE)
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!HasItemEquiped("RR#GUARA",Player1) // The Guardian
	!HasItemEquiped("RR#GUARA",Player2) // The Guardian
	!HasItemEquiped("RR#GUARA",Player3) // The Guardian
	!HasItemEquiped("RR#GUARA",Player4) // The Guardian
	!HasItemEquiped("RR#GUARA",Player5) // The Guardian
	!HasItemEquiped("RR#GUARA",Player6) // The Guardian
	!HasItemEquiped("RR#GUARB",Player1) // The Guardian +2
	!HasItemEquiped("RR#GUARB",Player2) // The Guardian +2
	!HasItemEquiped("RR#GUARB",Player3) // The Guardian +2
	!HasItemEquiped("RR#GUARB",Player4) // The Guardian +2
	!HasItemEquiped("RR#GUARB",Player5) // The Guardian +2
	!HasItemEquiped("RR#GUARB",Player6) // The Guardian +2
	!HasItemEquiped("RR#GUARC",Player1) // The Guardian +3
	!HasItemEquiped("RR#GUARC",Player2) // The Guardian +3
	!HasItemEquiped("RR#GUARC",Player3) // The Guardian +3
	!HasItemEquiped("RR#GUARC",Player4) // The Guardian +3
	!HasItemEquiped("RR#GUARC",Player5) // The Guardian +3
	!HasItemEquiped("RR#GUARC",Player6) // The Guardian +3
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringNoName(Player1,@71) // You are unable retain the mental link with the shambler without holding the shield.
		DisplayString(Myself,14065) // Unsummoned
		CreateVisualEffectObject("SPGFLSH1",Myself)
		SetInterrupt(TRUE)
		DestroySelf()
END

IF
	NumCreatureGT("RR#SHM",1)
	!Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayString(Myself,14065) // Unsummoned
		DisplayStringNoName(Player1,74090) // You cannot summon another creature of that type at this time.
		CreateVisualEffectObject("SPGFLSH1",Myself)
		SetInterrupt(TRUE)
		DestroySelf()
END

IF
	Global("RR#SHMUPG","GLOBAL",0)
	LevelGT(Myself,15)
	InParty(LastSummonerOf(Myself))	
	Class(LastSummonerOf(Myself),DRUID_ALL)
	HasItemEquipedReal("RR#GUARA",LastSummonerOf(Myself)) // The Guardian
	Detect(LastSummonerOf(Myself))
	!Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("RR#SHMUPG","GLOBAL",1)
		TakeItemReplace("RR#GUARB","RR#GUARA",LastSummonerOf(Myself)) // The Guardian +2
		DisplayStringNoNameHead(Myself,@402) // Your bond with Nature strengthens and the power of the Guardian grows.
		PlaySound("EFF_M72C")
		SetInterrupt(TRUE)
END

IF
	Global("RR#SHMUPG","GLOBAL",1)
	LevelGT(Myself,19)
	InParty(LastSummonerOf(Myself))	
	Class(LastSummonerOf(Myself),DRUID_ALL)
	HasItemEquipedReal("RR#GUARB",LastSummonerOf(Myself)) // The Guardian +2
	Detect(LastSummonerOf(Myself))
	!Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("RR#SHMUPG","GLOBAL",2)
		TakeItemReplace("RR#GUARC","RR#GUARB",LastSummonerOf(Myself)) // The Guardian +3
		DisplayStringNoNameHead(Myself,@402) // Your bond with Nature strengthens and the power of the Guardian grows.
		PlaySound("EFF_M72C")
		SetInterrupt(TRUE)
END

IF
	!InActiveArea(Myself)
THEN
	RESPONSE #100
		NoAction()
END

IF
	Allegiance(Myself,EVILCUTOFF)
	OR(2)
		See([GOODCUTOFF])
		See(LastSummonerOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),15)
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!StateCheck(Myself,STATE_INVISIBLE)
	!Global("dvgldust","LOCALS",1)
	CombatCounter(0)
	!See([EVILCUTOFF])
	OR(5)
		AreaType(FOREST)
		AreaCheck("AR0043") // Random encounter (forest)
		AreaCheck("AR1700") // Small teeth pass (marsh)
		AreaCheck("AR1900") // Druid Grove (marsh)
		AreaCheck("AR6400") // North Forest in ToB (marsh)
THEN
	RESPONSE #100
		DisplayString(Myself,@69) // blends into natural surroundings
		ApplySpellRES("RR#INVIS",Myself)
		PlaySound("ACT_07")
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty([EVILCUTOFF])
	See([EVILCUTOFF])
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(SixthNearest([EVILCUTOFF]))
	!CheckStatGT(SixthNearest([EVILCUTOFF]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT(SixthNearest([EVILCUTOFF]),1,STONESKINS)
	CheckStatLT(SixthNearest([EVILCUTOFF]),100,RESISTCRUSHING)
	!StateCheck(SixthNearest([EVILCUTOFF]),STATE_MIRRORIMAGE)
	!StateCheck(SixthNearest([EVILCUTOFF]),STATE_PANIC)
	See(SixthNearest([EVILCUTOFF]))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(FifthNearest([EVILCUTOFF]))
	!CheckStatGT(FifthNearest([EVILCUTOFF]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT(FifthNearest([EVILCUTOFF]),1,STONESKINS)
	CheckStatLT(FifthNearest([EVILCUTOFF]),100,RESISTCRUSHING)
	!StateCheck(FifthNearest([EVILCUTOFF]),STATE_MIRRORIMAGE)
	!StateCheck(FifthNearest([EVILCUTOFF]),STATE_PANIC)
	See(FifthNearest([EVILCUTOFF]))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(FourthNearest([EVILCUTOFF]))
	!CheckStatGT(FourthNearest([EVILCUTOFF]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT(FourthNearest([EVILCUTOFF]),1,STONESKINS)
	CheckStatLT(FourthNearest([EVILCUTOFF]),100,RESISTCRUSHING)
	!StateCheck(FourthNearest([EVILCUTOFF]),STATE_MIRRORIMAGE)
	!StateCheck(FourthNearest([EVILCUTOFF]),STATE_PANIC)
	See(FourthNearest([EVILCUTOFF]))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(ThirdNearest([EVILCUTOFF]))
	!CheckStatGT(ThirdNearest([EVILCUTOFF]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT(ThirdNearest([EVILCUTOFF]),1,STONESKINS)
	CheckStatLT(ThirdNearest([EVILCUTOFF]),100,RESISTCRUSHING)
	!StateCheck(ThirdNearest([EVILCUTOFF]),STATE_MIRRORIMAGE)
	!StateCheck(ThirdNearest([EVILCUTOFF]),STATE_PANIC)
	See(ThirdNearest([EVILCUTOFF]))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(SecondNearest([EVILCUTOFF]))
	!CheckStatGT(SecondNearest([EVILCUTOFF]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT(SecondNearest([EVILCUTOFF]),1,STONESKINS)
	CheckStatLT(SecondNearest([EVILCUTOFF]),100,RESISTCRUSHING)
	!StateCheck(SecondNearest([EVILCUTOFF]),STATE_MIRRORIMAGE)
	!StateCheck(SecondNearest([EVILCUTOFF]),STATE_PANIC)
	See(SecondNearest([EVILCUTOFF]))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty([EVILCUTOFF])
	!CheckStatGT([EVILCUTOFF],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	CheckStatLT([EVILCUTOFF],1,STONESKINS)
	CheckStatLT([EVILCUTOFF],100,RESISTCRUSHING)
	!StateCheck([EVILCUTOFF],STATE_MIRRORIMAGE)
	!StateCheck([EVILCUTOFF],STATE_PANIC)
	See([EVILCUTOFF])
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InParty(LastSeenBy(Myself))
	Allegiance(LastSeenBy(Myself),EVILCUTOFF)
	HPGT(LastSeenBy(Myself),0)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),15)
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!See([EVILCUTOFF])
	ActionListEmpty()
	InParty(LastSummonerOf(Myself))
	!Range(LastSummonerOf(Myself),15)
	InMyArea(LastSummonerOf(Myself))
	InActiveArea(Myself)
	InActiveArea(LastSummonerOf(Myself))
THEN
	RESPONSE #100
		DisplayString(Myself,@400) // BY NATURE'S WILL, I ANSWER YOUR CALL!
		Formation(LastSummonerOf(Myself),[-4.-4])
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!See([EVILCUTOFF])
	ActionListEmpty()
	!InParty(LastSummonerOf(Myself))
	!Range(Player1,15)
	InMyArea(Player1)
	InActiveArea(Myself)
THEN
	RESPONSE #100
		Formation(Player1,[-4.-4])
END